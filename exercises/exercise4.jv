pipeline TemperaturePipeline {
    //01. Define the structure of the pipeline by connecting blocks
    TemperatureExtractor
        -> TemperatureFilePicker
        -> TemperatureTextFileInterpreter
        -> TemperatureCSVInterpreter;

    TemperatureCSVInterpreter
        -> TemperatureCellWriter01
        -> TemperatureCellWriter02
        -> TemperatureTableInterpreter
        -> CelsiusToFahrenheitTransformer01
        -> CelsiusToFahrenheitTransformer02
        -> TemperatureSQLiteLoader;

    //02.Block to download and unzip the data
    block TemperatureExtractor oftype GTFSExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    //03.Block to specify the path to the extracted CSV file
    block TemperatureFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    //04. Block to interpret the text file with utf8 encoding
    block TemperatureTextFileInterpreter oftype TextFileInterpreter {
        encoding: "utf8";
    }

    //05. Block to interpret the CSV file with ; as delimiter
    block TemperatureCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }
    
    //06. Block to write headers in the specified cell range
    block TemperatureCellWriter01 oftype CellWriter {
        at: range A1:E1;
        write: ['id', 'producer', 'model', 'month', 'temperature'];
    }

    //07. Block to write the header for battery_temperature
    block TemperatureCellWriter02 oftype CellWriter {
        at: cell J1;
        write: ['battery_temperature'];
    }

    //08 Block to define the transformation from Celsius to Fahrenheit
    transform CelsiusToFahrenheit {
        from Celsius oftype decimal;
        to Fahrenheit oftype decimal;
        Fahrenheit: (Celsius * 9/5) + 32;
    }

    //09. Block to apply the transformation to the temperature column
    block CelsiusToFahrenheitTransformer01 oftype TableTransformer {
        inputColumns: ['temperature'];
        outputColumn: 'temperature';
        use: CelsiusToFahrenheit;
    }

    //10. Block to apply the transformation to the battery_temperature column
    block CelsiusToFahrenheitTransformer02 oftype TableTransformer {
        inputColumns: ['battery_temperature'];
        outputColumn: 'battery_temperature';
        use: CelsiusToFahrenheit;
    }

    //11. Block to define the schema of the SQLite table
    block TemperatureTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype PositiveInteger,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    //12. Block to load the data into a SQLite table
    block TemperatureSQLiteLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }
}
    // 13. This constraint ensures that the id column is a positive integer.
    valuetype PositiveInteger oftype integer {
        constraints: [
            OnlyValidNumbers,
        ];
    }

    

    // 14. This constraint ensures that the all the integers entries are positive.
    constraint OnlyValidNumbers on integer:
        value >= 0;
